program tei_debug
  implicit none

  !===============================
  !  CONSTANTS I PARÀMETRES
  !===============================
  integer, parameter :: dp = kind(1.0d0)
  integer, parameter :: N = 101

  real(dp), parameter :: ta = 0.025_dp
  real(dp), parameter :: l = 0.02_dp
  real(dp), parameter :: cv = 3686.0_dp
  real(dp), parameter :: rho = 1081.0_dp
  real(dp), parameter :: kappa = 0.56_dp
  real(dp), parameter :: sigma = 0.472_dp
  real(dp), parameter :: V = 40.0_dp
  real(dp), parameter :: Tcorporal = 309.65_dp

  integer, parameter :: dt_mode = 1   ! 1:dt=dx  2:dt=0.5dx^2 

  !===============================
  !  VARIABLES
  !===============================
  real(dp) :: Pext
  real(dp) :: xnorm
  real(dp) :: t_norm
  real(dp) :: x
  real(dp) :: Tnorm
  real(dp) :: dx, dt, T0, gamma
  integer :: nt
  integer :: i, j, ierr

  !===============================
  !  ARRAYS
  !===============================
  real(dp), allocatable :: vec(:,:)   ! (nt, N)
  real(dp), allocatable :: T(:)       ! (N-2)
  real(dp), allocatable :: rhs(:)
  real(dp), allocatable :: a(:), b(:), c(:)
  real(dp), allocatable :: cp(:), dpv(:)

  !===============================
  !  INICIALITZACIONS
  !===============================
  xnorm = l
  x = l / xnorm
  t_norm = l*l*cv*rho / kappa
  Pext = V**2 * sigma / (2.0_dp * l*l)
  Tnorm = Pext * l*l / kappa

  dx = l / xnorm / real(N-1,dp)

  select case (dt_mode)
    case (1)
      dt = dx*dx
    case (2)
      dt = 0.5_dp * dx*dx
  end select

  gamma = dt / (dx*dx)
  T0 = Tcorporal / Tnorm

  nt = int(ta / dt + 0.5_dp) + 1

  write(*,*) "Paràmetres: N=", N, " dx=", dx, " dt=", dt
  write(*,*) "gamma=", gamma, " nt=", nt
  write(*,*) "Tnorm=", Tnorm, " T0(adim)=", T0

  !===============================
  !  ALLOCATE ARRAYS
  !===============================
  allocate(vec(nt, N), stat=ierr); if (ierr /= 0) stop "Error allocant vec"
  allocate(T(N-2),     stat=ierr); if (ierr /= 0) stop "Error allocant T"
  allocate(rhs(N-2),   stat=ierr); if (ierr /= 0) stop "Error allocant rhs"
  allocate(a(N-2),     stat=ierr); if (ierr /= 0) stop "Error allocant a"
  allocate(b(N-2),     stat=ierr); if (ierr /= 0) stop "Error allocant b"
  allocate(c(N-2),     stat=ierr); if (ierr /= 0) stop "Error allocant c"
  allocate(cp(N-2),    stat=ierr); if (ierr /= 0) stop "Error allocant cp"
  allocate(dpv(N-2),   stat=ierr); if (ierr /= 0) stop "Error allocant dpv"

  !===============================
  !  CONDICIONS DE CONTORN
  !===============================
  do i = 1, nt
    vec(i,1) = T0
    vec(i,N) = T0
  end do

  do j = 1, N-2
    vec(1,j+1) = T0
    T(j) = T0
  end do

  !===============================
  !  MATRÍU TRIDIAGONAL
  !===============================
  do j = 1, N-2
    b(j) = 1.0_dp + 2.0_dp*gamma
    a(j) = -gamma
    c(j) = -gamma
  end do

  a(1) = 0.0_dp
  c(N-2) = 0.0_dp

  !comprovo que els valors de b no són massa petits
  do j = 1, N-2
    if (abs(b(j)) < 1.0e-16_dp) stop "Error: diagonal b quasi zero"
  end do

  !===============================
  !  BUCLE TEMPORAL
  !===============================
  do i = 2, nt

    do j = 1, N-2
      rhs(j) = vec(i-1, j+1) + dt
    end do

    rhs(1)     = rhs(1)     + gamma * vec(i-1,1)
    rhs(N-2) = rhs(N-2) + gamma * vec(i-1,N)

    ! Forward sweep TDMA
    cp(1) = c(1) / b(1)
    dpv(1) = rhs(1) / b(1)

    do j = 2, N-2
      if (abs(b(j) - a(j)*cp(j-1)) < 1.0e-16_dp) then
        write(*,*) "Numerical breakdown at j=", j
        stop
      end if

      cp(j)  = c(j) / (b(j) - a(j)*cp(j-1))
      dpv(j) = (rhs(j) - a(j)*dpv(j-1)) / (b(j) - a(j)*cp(j-1))
    end do

    ! Back-substitution
    T(N-2) = dpv(N-2)
    do j = N-3, 1, -1
      T(j) = dpv(j) - cp(j)*T(j+1)
    end do

    do j = 1, N-2
      vec(i, j+1) = T(j)
    end do

    ! Debug
    if (mod(i, max(1, nt/10)) == 0 .or. i == 2 .or. i == nt) then
      write(*,'(A,I6,A,F10.6)') "i=", i, "  Tcentre(adim)=", vec(i,(N+1)/2)
      write(*,'(A,F10.6)') " Tcentre(C)=", vec(i,(N+1)/2)*Tnorm - 273.15_dp
    end if

  end do

  !===============================
  !  SORTIDA A FITXER
  !===============================
  open(unit=10, file="TEIm05.2.txt", status="replace", action="write", iostat=ierr)
  if (ierr /= 0) then
    write(*,*) "Error obrint fitxer TEIm05.2.txt"
  else
    do i = 1, nt
      do j = 1, N-1
        write(10,'(F12.6,A)', advance='no') (vec(i,j)*Tnorm - 273.15_dp), ", "
      end do
      write(10,'(F12.6)') (vec(i,N)*Tnorm - 273.15_dp)
    end do
    close(10)
    write(*,*) "Sortida escrita a TEIm05.2.txt"
  end if

  !===============================
  !  FINAL
  !===============================
  deallocate(vec, T, rhs, a, b, c, cp, dpv)

end program tei_debug
