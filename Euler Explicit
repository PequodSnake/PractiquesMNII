program euler
  implicit none

  integer, parameter :: dp   = kind(1.0d0)
  integer, parameter :: npts = 101


  integer :: j, istep, nsteps

  
  real(dp) :: dz, dt, cfl
  real(dp) :: t_final, time_val
  real(dp) :: t_norm, Tnorm, Pext
  real(dp) :: T0_adim, l, cv, rho, kappa, sigma, V, Tcorporal
  real(dp) :: time_real


  real(dp), dimension(npts) :: z
  real(dp), dimension(npts) :: temp, temp_new

  l          = 0.02_dp
  cv         = 3686.0_dp
  rho        = 1081.0_dp
  kappa      = 0.56_dp
  sigma      = 0.472_dp
  V          = 40.0_dp
  Tcorporal  = 309.65_dp      


  t_norm = l*l*cv*rho / kappa
  Pext   = V**2 * sigma / (2.0_dp * l*l)
  Tnorm  = Pext * l*l / kappa

  T0_adim = Tcorporal / Tnorm

  dz = 1.0_dp / real(npts-1, dp)

  do j = 1, npts
     z(j)    = real(j-1, dp) * dz      
     temp(j) = T0_adim                 
  end do

  
  cfl = 0.49_dp             
  dt  = cfl * dz * dz       

  t_final = 0.025_dp        
  nsteps  = int(t_final/dt + 0.5_dp)

  time_val = 0.0_dp        

  do istep = 1, nsteps

     
     temp_new(1)    = T0_adim
     temp_new(npts) = T0_adim

    
     do j = 2, npts-1
        temp_new(j) = temp(j) + dt * ( ( temp(j+1) - 2.0_dp*temp(j) + temp(j-1) )/(dz*dz) + 1.0_dp )
     end do

     temp     = temp_new
     time_val = time_val + dt

  end do

  time_real = time_val * t_norm


  print *, "cfl           =", cfl
  print *, "t_final(adim) =", time_val
  print *, "t_final(real) =", time_real, " s"
  print *, "T centre (K)  =", temp(npts/2)*Tnorm
  print *, "T centre (C)  =", temp(npts/2)*Tnorm - 273.15_dp
  print *, "Tmax (K)      =", maxval(temp)*Tnorm
  print *, "Tmin (K)      =", minval(temp)*Tnorm

  
  do j = 1, npts
     z(j)    = z(j) * l
     temp(j) = temp(j)*Tnorm - 273.15_dp
  end do

  call guarda_dades(z, temp, npts, 'T_real_expl_cfl025.dat')

contains

  subroutine guarda_dades(x, y, n, nom)
    implicit none
    integer, intent(in)      :: n
    real(dp), intent(in)     :: x(n), y(n)
    character(len=*), intent(in) :: nom
    integer :: j, iu

    iu = 10
    open(unit=iu, file=nom, status='replace', action='write')
    do j = 1, n
       write(iu,*) x(j), y(j)
    end do
    close(iu)
  end subroutine guarda_dades

end program euler
